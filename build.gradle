plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'groovy'
if (project.hasProperty('signing.keyId')) {
    apply plugin: 'signing'
}

def currentversion = '0.2.18'
group = 'com.kayako'
version = currentversion


allprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.11.3'
    compile 'com.squareup.okhttp3:okhttp:3.12.0'

    testCompile 'org.codehaus.groovy:groovy-all:2.5.4'
    testCompile "org.spockframework:spock-core:1.2-groovy-2.5"

    // optional dependencies for using Spock
    testRuntime "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
    testRuntime "cglib:cglib-nodep:3.2.9"
    // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:3.0.1"
    // allows mocking of classes without default constructor (together with CGLIB)
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

repositories {
    mavenCentral()
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

publishing {
    publications {
        JavaPhoenixChannelsJar(MavenPublication) {
            from components.java
            groupId 'com.kayako.kre'
            artifactId 'kre-client'
            version currentversion
        }
    }
}

bintray {
    user = 'kayako'
    key = 'ADD_API_HERE'
    pkg {
        repo = 'kayako-devkits'
        name = 'kayako-kre-client'
        userOrg = 'kayako-support'
        licenses = ['BSD-3']
        vcsUrl = 'https://github.com/kayako/kre-java-client'
    }
    publications = ['JavaPhoenixChannelsJar']
}
